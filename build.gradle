plugins {
    id 'org.springframework.boot' version '2.4.3'
    id "io.spring.dependency-management" version "1.0.11.RELEASE"
    id 'java'
    id 'checkstyle'
    id 'jacoco'
    id 'pmd'
    id "io.freefair.lombok" version "5.3.3.3"
    id "org.flywaydb.flyway" version "7.8.1"
}

project(":") {
    group = 'com.scaffold'
    version = '1.0.0'
    sourceCompatibility = '15'

    repositories {
        jcenter()
        mavenCentral()
    }

    pmd {
        toolVersion = '6.31.0'
        ruleSetFiles = files('config/pmd.xml')
        ruleSets = []
        consoleOutput = true
    }

    checkstyleMain {
        configFile = file('config/checkstyle.xml')
        maxWarnings = 0
        maxErrors = 0
    }

    checkstyleTest {
        configFile = file('config/checkstyle.xml')
        maxWarnings = 0
        maxErrors = 0
    }

    jacoco {
        toolVersion = "0.8.6"
    }

    jacocoTestReport {
        reports {
            xml.enabled false
            html.enabled true
        }

        afterEvaluate {
            classDirectories.setFrom(files(classDirectories.files.collect {
                fileTree(dir: it,
                        exclude: [
                                'com/scaffold/BootApplication.class',
                                'com/scaffold/api/diagnosis/DatabaseHealthCheck.class',
                                'com/scaffold/api/IndexController'
                        ]
                )
            }))
        }
    }

    jacocoTestCoverageVerification {
        dependsOn 'jacocoTestReport'

        violationRules {
            rule {
                element = 'CLASS'
                limit {
                    minimum = 1.0
                }
                excludes = [
                        'com.scaffold.BootApplication',
                        'com.scaffold.api.diagnosis.DatabaseHealthCheck',
                        'com.scaffold.api.IndexController'
                ]
            }
        }
    }

    lombok {
        version = "1.18.16"
    }

    test {
        useJUnitPlatform()
        exclude "com/scaffold/integration/**"
        testLogging {
            events "passed", "skipped", "failed"
        }
    }

    flyway {
        url = "jdbc:mysql://${project.findProperty("dbHost") ?: "localhost"}:3306/scaffold_db"
        user = 'scaffold_admin'
        password = 'password10'
    }

    task integrationTest(type: Test) {
        useJUnitPlatform()
        include "com/scaffold/integration/**"

        beforeTest { descriptor ->
            logger.lifecycle("Running test: " + descriptor)
        }

        onOutput { descriptor, event ->
            logger.lifecycle(event.message)
        }
    }

    ext {
        NEWRELIC = 'com.newrelic.agent.java:newrelic-agent:6.4.2'
    }

    configurations {
        newrelic
        developmentOnly
        runtimeClasspath {
            extendsFrom developmentOnly
        }
    }

    ext {
        set('springCloudVersion', "2020.0.1")
    }

    dependencies {
        implementation 'com.google.guava:guava:30.1.1-jre'
        implementation 'org.springframework.boot:spring-boot-starter'
        implementation 'org.springframework.boot:spring-boot-starter-web'
        implementation 'org.springframework.cloud:spring-cloud-starter-openfeign'
        implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
        implementation 'org.springframework.boot:spring-boot-starter-hateoas'
        implementation 'org.springframework.boot:spring-boot-starter-validation'
        implementation 'mysql:mysql-connector-java:8.0.24'
        implementation 'org.flywaydb:flyway-core:7.+'
        implementation 'net.logstash.logback:logstash-logback-encoder:6.+'
        implementation 'commons-beanutils:commons-beanutils:1.9.4'
        implementation 'com.vladmihalcea:hibernate-types-52:2.10.3'

        testImplementation('org.springframework.boot:spring-boot-starter-test') {
            exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
        }
        testImplementation "org.testcontainers:mysql:1.15.3"
        testImplementation 'io.rest-assured:json-schema-validator:4.3.3'
        testImplementation 'com.github.tomakehurst:wiremock:2.25.1'

        developmentOnly 'org.springframework.boot:spring-boot-devtools'
        compileOnly "io.springfox:springfox-swagger2:3.0.0"

        newrelic(NEWRELIC)
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        }
    }

    bootRun {
        jvmArgs = ["-Xdebug", "-Xrunjdwp:transport=dt_socket,server=y,suspend=n,address=0.0.0.0:5005"]
    }

    task copyNewrelicAgent(type: Copy) {
        from configurations.newrelic
        into 'build/libs/'
        rename { String fileName ->
            "newrelic.jar"
        }
    }

    check {
        dependsOn 'integrationTest'
        dependsOn 'jacocoTestReport'
        dependsOn 'jacocoTestCoverageVerification'
        dependsOn 'pmdMain'
        dependsOn 'pmdTest'
    }

    processResources {
        dependsOn 'copyNewrelicAgent'
    }
}
